#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_gpu_shader_fp64 : enable

#define WORKGROUP_SIZE 32

layout (local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

struct Particle {
	dvec3 position;
	double mass;
	dvec3 prevPosition;
	double invMass;
	dvec3 velocity;
	double null;
//	dvec4 null; // external force?
};

layout(std430, set = 0, binding = 0) buffer particlesBuffer {
	Particle[] particles;
};

readonly layout(set = 1, binding = 0) buffer constraintParticleIndices {
	int[] particleIndices;
};

readonly layout(set = 1, binding = 1) buffer constraintRestDistances {
	double[] restDistances;
};

readonly layout(push_constant) uniform constants {
	double dt;
	double invDtSquared;
	double compliance;
	int count;
	int	constraintOffset;
};

void main() {
	if (gl_GlobalInvocationID.x >= count) return;

	int index1 = particleIndices[(gl_GlobalInvocationID.x + constraintOffset) * 2];
	int index2 = particleIndices[(gl_GlobalInvocationID.x + constraintOffset) * 2 + 1];

	dvec3 distanceVector = particles[index2].position - particles[index1].position;
	double distanceDifference = length(distanceVector) - restDistances[gl_GlobalInvocationID.x + constraintOffset];

//	if (abs(distanceDifference) < 0.00001) return;

	double lambda = 1.0 / (particles[index1].invMass + particles[index2].invMass + compliance * invDtSquared);
	dvec3 normalizedPositionDifference = normalize(distanceVector) * (lambda * distanceDifference);

	particles[index1].position += particles[index1].invMass * normalizedPositionDifference;
	particles[index2].position -= particles[index2].invMass * normalizedPositionDifference;
}